plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.runtime' version '1.12.5'
}

version = '0.1.0'

mainClassName = 'com.glencoesoftware.convert.Launcher'
applicationName = 'NGFF Converter GUI'

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url 'https://repo.glencoesoftware.com/repository/bioformats2raw2ometiff/'
    }
    maven {
        url 'https://repo.glencoesoftware.com/repository/jzarr-snapshots'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.releases/'
    }
    maven {
        url 'https://maven.scijava.org/content/groups/public'
    }
}

sourceCompatibility = 16
targetCompatibility = 16

javafx {
    version = '17.0.1'
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.graphics' ]
}

dependencies {
    implementation 'com.glencoesoftware:bioformats2raw:0.4.0'
    implementation 'ome:formats-gpl:6.8.0'
    implementation 'info.picocli:picocli:4.6.2'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.0'
    implementation 'org.kordamp.ikonli:ikonli-bootstrapicons-pack:12.3.0'
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"
}

configurations.all {
    exclude group: 'edu.ucar', 'module': 'cdm'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'NGFF Converter GUI',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    targetPlatform("lin") {
        jdkHome = jdkDownload("https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.2%2B8/OpenJDK17U-jdk_x64_linux_hotspot_17.0.2_8.tar.gz")
    }
    targetPlatform("mac") {
        jdkHome = jdkDownload("https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.2%2B8/OpenJDK17U-jdk_x64_mac_hotspot_17.0.2_8.tar.gz") {
            downloadDir = "$buildDir/myMac"
            archiveName = "my-mac-jdk"
            archiveExtension = "tar.gz"
            pathToHome = "jdk-17.0.2+8/Contents/Home"
            overwrite = true
        }
    }
    targetPlatform("win") {
        jdkHome = jdkDownload("https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.2%2B8/OpenJDK17U-jdk_x64_windows_hotspot_17.0.2_8.zip")
    }


    launcher {
        noConsole = true
    }
    jpackage {

        imageName = applicationName

        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/com/glencoesoftware/convert/main-icon.$imgType"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Glencoe Software']
        installerOptions += ['--app-version', version]
        installerOptions += ['--description', "Converts image files to NGFF .zarr"]

        if(currentOs.windows) {
            targetPlatformName = "win"
            installerOptions += ['--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
        else if (currentOs.linux) {
            targetPlatformName = "lin"
            installerOptions += ['--linux-package-name', applicationName,'--linux-shortcut']
        }
        else if (currentOs.macOsX) {
            targetPlatformName = "mac"
            installerOptions += ['--mac-package-name', applicationName]
        }
    }
}
